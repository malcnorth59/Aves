Name     Aves_Memory_Decoder;
PartNo   ATF22V10C;
Date     2025;
Revision 1.0;
Designer Malcolm North;
Company  None;
Assembly Aves Computer;
Location None;
Device   g22v10;

/*********************************************************************
 * Video Memory Access Controller (VMAC)
 * 
 * Purpose:
 * Controls memory access arbitration between the CPU and video controller
 * supporting both VIC (6561) and VIC-II (6569) timing requirements.
 *
 * Timing Generation:
 * - External shift register delays the AEN signal
 * - Delayed AEN is OR'ed with original AEN to create timing slots
 * - This creates a pulse at the end of each clock cycle
 * - tsa/tsb are derived from this delayed timing chain
 *
 * VIC-II (6569) Operation:
 * - AEN indicates when VIC-II has control of the bus
 * - Can occur in both clock phases
 * - Shift register creates necessary timing margins
 *
 * VIC (6561) Operation:
 * - Video access occurs only during PHI1 (clock phase 1)
 * - PHI2 clock is used as input for AEN
 * - Shift register creates necessary timing margins
 *
 * ATF22V10C Pinout:
 *                    ___________
 *            aen  1 |           | 24 VCC
 *            tsa  2 |           | 23 vacc
 *            tsb  3 |           | 22 exba
 *          iordy  4 |           | 21 vaccb    
 *        exrdyin  5 |           | 20  
 *                 6 |           | 19  
 *                 7 |           | 18  
 *                 8 |           | 17  
 *                 9 |           | 16  
 *                10 |           | 15 exrdy  
 *                11 |           | 14 ready
 *            GND 12 |           | 13 vpa
 *                   |___________|
 *
 *********************************************************************/

/* *************** INPUT PINS *********************/
PIN 1 = aen;        /* Address Enable from video controller
                     *
                     * High indicates CPU has the BUS
                     * Low indicates VIC-II bus access
                     *
                     * 6569: Connected to AEN on VIC-II
                     * 6561: Connected to PHI2 clock */
PIN 2 = tsa;        /* Timeslot A - Generated from delayed AEN
                     * Creates timing margin at end of CPU access, for ready signal */
PIN 3 = tsb;        /* Timeslot B - Generated from delayed AEN
                     * Creates timing margin at end of video access, for address bus enable signal */
pin 4 = iordy;      /* I/O Ready - Active low wait state request */
pin 5 = exrdyin;    /* External Ready In - Active low wait state request */

/* *************** OUTPUT PINS ********************/
pin 14 = ready;     /* System Ready - Generated from timeslot margins
                     * Ensures clean transitions between CPU/video access */
pin 15 = exrdy;     /* External Ready - Propagates ready state */
pin 21 = vaccb;      /* vacc inverted for selecting tristate buffers */
pin 22 = exba;      /* External Bus Access - CPU bus control indicator */
pin 23 = vacc;      /* Video Access - Active during video controller slot
                     * Derived from tsb and aen to ensure proper timing */

/* *************** LOGIC EQUATIONS *********************/

/* Ready signal generation:
 * - Uses tsa (derived from delayed AEN) to create timing margin
 * - Ensures clean handover between CPU and video access
 * - Additional wait states from iordy/exrdyin
 */
ready = (tsa # aen) & iordy & exrdyin;

/* Video access control:
 * - Active during video timeslot (tsb) or direct AEN
 * - tsb provides timing margin for clean bus handover
 */
vacc  = !(tsb # aen);
vaccb  = (tsb # aen);

/* External ready propagation */
exrdy = exrdyin & ready;

/* External bus access control:
 * - Indicates when CPU has control of external bus
 * - Derived from video timing signals
 */
exba  = (tsb # aen);

/* Notes:
 * - External shift register is critical for proper timing generation
 * - System automatically adapts to VIC/VIC-II timing differences
 * - Unused pins configured as inputs with pull-ups
 */


